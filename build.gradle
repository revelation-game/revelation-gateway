plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'

    // For test & quality control
    id 'jacoco'
    id "org.sonarqube" version "3.1.1"

}

// Load version from file
//def buildProperties = new Properties()
//file("version.properties").withInputStream { stream ->
//    buildProperties.load(stream)
//}
version = '0.0.1-SNAPSHOT'
group = 'io.github.revelation-game'
sourceCompatibility = '15'


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2020.0.1")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.5.5'

    // For Constructor, Getter and Setter annotations
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // For auto completion on properties
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // ---- Test Dependencies ----

    // Test dependencies
    testImplementation('org.projectlombok:lombok')
    testImplementation 'commons-io:commons-io:2.8.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Test annotations
    testAnnotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// --- Setup Tests ---

// Define jacoco reports for tests
jacocoTestReport {
    reports {
        xml.enabled true
    }
}

// Define integration test
sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

test {}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

// Test and integrationTest should print logging events and use JUnit
tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardError" //, "standardOut"
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
//    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

check.dependsOn integrationTest

// --- Setup Sonar ---
sonarqube {
    properties {
        property "sonar.projectKey", "revelation-game_revelation-gateway"
        property "sonar.organization", "revelation-game"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}